<html>
  <head>
    <title>TSP-GA Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    
    u = ABM.util

    class MyModel extends ABM.Model
      setup: ->
        @agentBreeds ["nodes", "travelers"]
        
        # no optimizations: 44fps
        @refreshPatches = false # for static patches
        @refreshAgents = false # for static agents

        # globals
        @nodeCount = 50
        @travelersCount = 100
        @growPopulation = true
        @useInversion = true
        @bestTourNodes = []
        @bestTourLength = 0
        @bestTourTick = 0
        @stopTickDifference = 500
        
        @animator.setRate 10, true
        
        # defaults
        @patches.setDefault "color", [250, 250, 0]
        @nodes.setDefault "shape", "circle"
        @nodes.setDefault "color", [255, 0, 0]
        @nodes.setDefault "heading", 0 # override promotion to random angle
        @travelers.setDefault "hidden", true
        @links.setDefault "color", [255, 0, 0]

        @patches.create()
        @setupNode n for n in @nodes.create @nodeCount
        
        @createTourLinks @nodes #()
        @bestTourLength = @links.reduce ((sum,l) -> sum + l.length()), 0
        
        @travelers.create @travelersCount, (a) => @setupTraveler(a)

      setupNode: (agent) =>
        agent.moveTo @patches.randomPoint()
        while @nodes.neighboring(agent, radius: 2).any()
          agent.moveTo @patches.randomPoint()

      setupTraveler: (agent) =>
        agent.tourNodes = @nodes.clone().shuffle() # ()
        agent.tourLength = @lengthFromNodes agent.tourNodes
        
      step: ->
        console.log @animator.toString() if @animator.ticks % 100 is 0

        for agent in @travelers #()
          @makeTour agent

        @installBestTour()

        if (@animator.ticks - @bestTourTick) is @stopTickDifference
          console.log "Stopping at tick #{@animator.ticks} after no change in" +
            " #{@stopTickDifference} ticks"
          console.log "Best tour: #{@bestTourLength} at tick #{@bestTourTick}"
          @stop()
      
      createTourLinks: (nodeList) ->
        @links.clear()
        @links.create nodeList[0], nodeList.last()
        @links.create nodeList[i], nodeList[i - 1] for i in [1...nodeList.length]
        # @links.setProperty "color", [255,0,0]
        
      lengthFromNodes: (nodeList) ->
        len = nodeList[0].distance(nodeList.last().position)

        for i in [1...nodeList.length]
          len += nodeList[i].distance(nodeList[i - 1].position)

        len

      installBestTour: ->
        # @travelers().max("tourLength").die() while @travelers().length>@travelersCount
        # a = @travelers().min("tourLength")
        @travelers.max("tourLength").die() while @travelers.length>@travelersCount
        a = @travelers.min("tourLength")
        if a.tourLength < @bestTourLength
          @reportNewTour a
          @bestTourLength = a.tourLength
          @bestTourNodes = a.tourNodes
          @bestTourTick = @animator.ticks
          @createTourLinks @bestTourNodes

      makeTour: (a) ->
        nlist = if @useInversion then @inversionStrategy a else @randomStrategy a
        len = @lengthFromNodes nlist
        if @growPopulation
        then a.hatch 1, @travelers, (a) => a.tourNodes = nlist; a.tourLength = len
        else (a.tourNodes = nlist; a.tourLength = len) if len < a.tourLength

      randomStrategy: (a) ->
        a.tourNodes.clone().shuffle()

      inversionStrategy: (a) ->
        ABM.Set.from @newInversion a.tourNodes

      newInversion: (nlist) ->
        len = nlist.length
        i = u.randomInt len - 1
        len = 2 + u.randomInt len - i - 2
        [].concat (nlist.slice 0, i),
                  (nlist.slice i, i + len).reverse(),
                  (nlist.slice i + len)

      reportNewTour: (a) ->
        console.log "new best tour at tick #{@animator.ticks}: #{a.tourLength} by traveler #{a.id}"
        
    # div, patchSize, minX, maxX, minY, maxY, isTorus, hasNeighbors
    #   Defaults: 13, -16, 16, -16, 16, false, true
    model = new MyModel {
      div: "layers",
      patchSize: 16,
      mapSize: 28,
      hasNeighbors: false
    }
    model.debug() # Debug: Put Model vars in global name space
    model.start() # Run model immediately after startup initialization
    
    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
