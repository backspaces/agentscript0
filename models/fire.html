<html>
  <head>
    <title>Fire Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.util

    class MyModel extends ABM.Model
      setup: ->
        @agentBreeds ["embers", "fires"]
        @agents.setDefault "shape", "square"
        @agents.setDefault "color", [255, 0, 0]
        @agents.setDefault "heading", 0 # override promotion to random angle
        
        @refreshPatches = false # <1fps->50-55fps, no surprise, app optimized for this 
        # No optimizations: 50-55fps
        # @patches.usePixels() # 50-55fps .. not used, refresh off
        # None of the optimizations particularly useful, other than refresh
        # If refresh on, fastPatches is in the 50+fps range.
        
        @density = 60 # percent
        @burnedTrees = 0
        @initialTrees = 0
        
        # defaults
        # @animator.setRate 10, true
        @animator.setRate 60, false
        
        for patch in @patches.create() when u.randomInt(100) < @density
          # u.setColor p.color, 0, 255, 0
          patch.color = [0, 255, 0] # override default, set per patch color

        for patch in @patches
          if patch.position.x is @patches.min.x
            @ignite patch

        @initialTrees = (p for p in @patches when u.colorsEqual p.color, [0, 255, 0]).length

        console.log "burnedTrees #{@burnedTrees}"

        @burnedTrees = 0 # reset from initial ignites

      ignite: (patch) ->
        patch.sprout 1, @fires
        patch.color = [0, 0, 0] # in original model but apparently not needed, refresh off?
        @burnedTrees++

      fadeEmbers: ->
        for ember in @embers by -1 # -1: allow die() in loop
          ember.color = u.fractionOfColor ember.color, .8
          if 100 > Math.max ember.color... # or (Math.max a.color...) < 100 , needs parens
            ember.patch.color = ember.color
            ember.patch.draw @contexts.patches
            ember.die()

      step: ->
        if @animator.ticks % 100 is 0
          console.log @animator.toString()

        unless @agents.any()
          console.log "..stopping, fire done at tick: #{@animator.ticks}"
          @stop()

        for fire in @fires by -1 # -1: allow changeBreed() in loop
          for patch in fire.patch.neighbors(diamond: 1)
            if u.colorsEqual patch.color, [0, 255, 0]
              @ignite patch

          @embers.setBreed fire

        @fadeEmbers()

    model = new MyModel {
      div: "layers",
      patchSize: 2,
      mapSize: 250
    }
    model.debug() # Debug: Put Model vars in global name space
    model.start() # Run model immediately after startup initialization
    
    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
