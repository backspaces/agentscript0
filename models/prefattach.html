<html>
  <head>
    <title>Pref-Attach Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    
    u = ABM.util

    class MyModel extends ABM.Model
      setup: ->
        @refreshPatches = false # for static patches
        
        # globals
        @circleLayout = true
        @histogram = []
        @stopTick = 400
        
        # defaults
        @agents.setDefault "shape", "circle"
        @links.setDefault "thickness", .75 # Easier to see thiner link
        
        @patches.create()
        @agents.create 1
        @makeNode @agents[0]

      step: ->
        @makeNode @findPartner()

        for agent in @agents
          agent.size = Math.sqrt agent.linkNeighbors().length

        @agents.formCircle @patches.max.x * 0.9

        @histogram = @agents.histogram 1, (agent) ->
          agent.linkNeighbors().length

        if @animator.ticks % 25 is 0
          console.log "Tick: #{@animator.ticks} Histogram: [#{@histogram}]"

        if @animator.ticks is @stopTick
          (console.log "Stopping at stopTick: #{@animator.ticks}"; @stop())

      makeNode: (toNode) ->
        @agents.create 1, (agent) =>
          @links.create agent, toNode
          agent.moveTo toNode.position
          agent.forward 8

      findPartner: ->
        @links.sample().bothEnds().sample()

    model = new MyModel {
      div: "layers",
      patchSize: 5,
      mapSize: 90,
      hasNeighbors: false
    }
    model.debug() # Debug: Put Model vars in global name space
    model.start() # Run model immediately after startup initialization

    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
