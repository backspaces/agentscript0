<html>
  <head>
    <title>Flock Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/dat.gui.min.js"></script>
    <script src="../lib/as.dat.gui.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    
    u = ABM.util

    class MyModel extends ABM.Model
      setVision: (@vision) ->

      setMaxTurn: (@maxTurn) ->
        @maxTurn = u.degreesToRadians(@maxTurn)

      toggleConsole: () ->
        @useConsole = not @useConsole
      
      setup: ->
        # u.randomSeed() # use for repeatable runs
      
        @refreshPatches = false # <3fps->4fps agents larger part of drawing 
        # No optimizations: 4fps
        # @patches.usePixels() # 4fps same as refresh off
        # w/ refresh off and two optimizations, 22-23fps
        @agents.setUseSprites() # 24 -> 36
        
        @animator.setRate 30, true # multistep
        
        @useConsole = true
        @population = 75      # agents
        @setVision 3          # patches & set patch rect
        @minSeparation = 0.75 # patches
        @setMaxTurn(3.0)      # degrees -> radians
      
        # defaults
        @agents.setDefault "size", 1.5
        # @agents.setDefault "color", [255,0,0] # slightly faster! ->38/39 ??

        @patches.create()

        for patch in @patches
          patch.color = u.randomGray 0, 100

        for patch in @patches.sample(@population)
          patch.sprout 1

        for agent in @agents
          agent.color = u.randomMapColor()
  
      step: ->
        if @useConsole and @animator.ticks % 25 is 0
          console.log @animator.toString(), "flock alignment: #{@reportFlockVectorSize()}"

        for agent in @agents
          @flock agent

        if @animator.draws is 2 # Show the sprite sheet if there is one after first draw
          sheet = u.shapes.spriteSheets.last() if u.shapes.spriteSheets.length isnt 0
          if sheet?
            console.log sheet if @useConsole
            document.getElementById("play").appendChild(sheet.canvas)
        # @stop() if @animator.ticks is 400

      flock: (agent) ->
        flockmates = agent.neighbors(@vision)

        if flockmates.length isnt 0
          [nearestNeighbor, distance] = flockmates.min(((f) ->
            f.distance agent.position), true)
          if distance < @minSeparation
            @separate agent, nearestNeighbor
          else
            @align agent, flockmates
            @cohere agent, flockmates

        agent.forward .5 # move partial patch for smooth animation.

      separate: (agent, nearest) ->
        angle = u.angle nearest.position, agent.position, @patches
        @turnTowards agent, angle

      align: (agent, flockmates) ->
        @turnTowards agent, @averageHeading(flockmates)

      cohere: (agent, flockmates) ->
        @turnTowards agent, @averageHeadingTowards(agent, flockmates)
        
      turnTowards: (agent, heading) ->
        turn = u.substractRadians heading, agent.heading # angle from h to a
        turn = u.clamp turn, -@maxTurn, @maxTurn # limit the turn
        agent.rotate turn

      averageHeading: (flockmates) ->
        dx = (Math.sin f.heading for f in flockmates).reduce (x, y) -> x + y
        dy = (Math.cos f.heading for f in flockmates).reduce (x, y) -> x + y
        @safeAtan dy, dx

      averageHeadingTowards: (agent, flockmates) ->
        dx = (Math.sin u.angle(f.position, agent.position, @patches) for f in flockmates).reduce (x, y) -> x + y
        dy = (Math.cos u.angle(f.position, agent.position, @patches) for f in flockmates).reduce (x, y) -> x + y
        @safeAtan dy, dx

      safeAtan: (x, y) ->
        Math.atan2 y, x # if x is 0 and y is 0 then 0 else Math.atan2 y, x # atan ok if x 0
      
      reportFlockVectorSize: ->
        dx = (Math.sin a.heading for a in @agents).reduce (x, y) -> x + y
        dy = (Math.cos a.heading for a in @agents).reduce (x, y) -> x + y
        Math.sqrt(dx * dx + dy * dy) / @population

    model = new MyModel({
      div: "layers",
      patchSize: 15,
      isTorus: true,
      hasNeighbors: false
    })
    .debug() # Debug: Put Model vars in global name space
    .start() # Run model immediately after startup initialization

    gui = new ABM.DatGUI(model, {
      "turning radius (deg)": {
        type: "slider",
        min: 0,
        max: 10,
        step: 0.5,
        val: 3,
        smooth: true,
        setter: "setMaxTurn"
      },
      "minSeparation": {
        type: "slider",
        min: 0,
        max: 10,
        step: 0.25,
        val: 0.75,
        smooth: true
      },
      "vision radius": {
        type: "slider",
        min: 0,
        max: 15,
        step: 1,
        val: 3,
        smooth: true,
        setter: "setVision"
      }
    })

    </script>
  </head>
  <body>
    <div id="layers"></div>
    <div id="play" style="float:right;"></div>
  </body>
</html>
