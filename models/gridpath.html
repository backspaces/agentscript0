<html>
  <head>
    <title>Grid Path Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.util

    class MyModel extends ABM.Model
      setup: ->
        @refreshAgents = false # for static agents
        
        # globals
        @prob = 1
        
        # defaults
        @patches.setDefault "labelColor", [255, 255, 255]
        offset = Math.floor @world.size * .45
        # @patches.setDefault "labelOffset", [12,14]
        @patches.setDefault "labelOffset", [offset, offset]
        u.contextTextParams @contexts.patches, "10px sans-serif", "right", "bottom"
        # u.elementTextParams @contexts.contexts.patches, "10px sans-serif", "right", "bottom"
        @agents.setDefault "shape", "circle"
        @agents.setDefault "color", ([210, 0, 0])
        @agents.setDefault "size", .4
        @agents.setDefault "hidden", 2.5
        @agents.setDefault "heading",  0 # override promotion to random angle

        @animator.setRate 10

        for patch in @patches.create()
          patch.color = u.randomGray 160, 190
          patch.occupied = false

        @drawGrid [0, 0, 0]
        @walker = @agents.create(1)[0]
        patch0 = @patches[0]
        @walker.moveTo patch0.position
        @walker.penDown = true
        # @walker.color = [210,0,0]
        # @walker.shape = "circle"
        # @walker.size = .4
        @walker.stamp()
        @walker.patch.occupied = true
        # @walker.penDown = true
        # @walker.penSize *= 2.5
        # @setTextParams @patches, "9px sans-serif", "right", "bottom"
        # @setLabelParams @patches, [255, 255, 255], [12, 12]

      step: ->
        @floodFill()
        patches = ABM.Set.from (neighbor for neighbor in @walker.patch.neighbors(diamond: 1) when neighbor.ok)
        @drawNum patches.length
        @walker.moveTo patches.sample().position
        @walker.stamp()
        @walker.patch.occupied = true
        if @done()
          @stop()
          console.log "prob: #{@prob}"
      
      drawNum: (number) ->
        @walker.patch.label = "" + number
        @prob /= number

      done: ->
        @patches.last().occupied
      
      drawGrid: (color) ->
        agent = @agents.create(1)[0]
        agent.color = color
        agent.penSize *= .4
        agent.heading = Math.PI / 2

        for x in [0..@patches.max.x]
          agent.moveTo x: x, y: 0
          agent.penDown = true
          agent.forward @patches.max.y
          agent.penDown = false
        agent.heading = 0

        for y in [0..@patches.max.y]
          agent.moveTo x: 0, y: y
          agent.penDown = true
          agent.forward @patches.max.x
          agent.penDown = false
        agent.die()
      
      floodFill: ->
        for patch in @patches
          patch.ok = false

        patches = new ABM.Set @patches.last()

        while patches.length > 0
          for patch in patches
            patch.ok = true

          neighbors = new ABM.Set

          for patch in patches
            neighbors.push patch.neighbors(diamond: 1)

          nextPatches = neighbors.flatten()
          nextPatches = nextPatches.flatten()
          nextPatches.sort("id").uniq()

          patches = new ABM.Set
          
          for nextPatch in nextPatches
            if not nextPatch.ok and not nextPatch.occupied
              patches.push nextPatch
        null

    new MyModel({
      div: "layers",
      patchSize: 30,
      min: {x: 0, y: 0},
      max: {x: 10, y: 10}
    })
    .start()
    .debug()

    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
