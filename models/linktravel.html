<html>
  <head>
    <title>Link Travel Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    
    u = ABM.util

    class MyModel extends ABM.Model
      setup: ->
        @agentBreeds ["nodes", "drivers"]
        @nodes.setDefault "shape", "circle"
        @nodes.setDefault "size", .3
        @drivers.setDefault "size", 1.5
        
        # 40-44fps -> 58+fps
        @refreshPatches = false # 56fps
        @refreshLinks = false # 40-49fps
        # @patches.usePixels() # 58+fps same as refresh off
        @agents.setUseSprites() # 24 -> 36
        # w/ refreshs off and 1 optimization 59/max-fps
        
        @animator.setRate 30, true # almost 3:1 steps/draws!
        # @animator.setRate 60, false # for optimization studies & profiling
        
        # globals
        @numNodes = 30
        @numDrivers = 100
        @layoutCircle = true
        @baseVelocity = 0.1 # patchs
        @velocityDelta = 0.1 # patches
        
        @links.setDefault "labelColor", [255, 0, 0]
        
        for patch in @patches.create()
          patch.color = u.randomGray 10, 50

        for patch in @patches.sample @numNodes
          patch.sprout 1, @nodes, (agent) =>
            if @nodes.length > 1 # if @nodes().length > 1
              @links.create agent, @nodes.other(agent).sample() # @links.create a, @nodes().other(a).sample()
        
        if @layoutCircle
          @nodes.formCircle @patches.max.x - 1
        
        # Test labels
        # l.label = l.length().toFixed(0) for l in @links
        
        # no defaults, hatch copies its values to new agent
        
        for i in [1..@numDrivers]
          node = @nodes.sample() # n = @nodes().sample()
          node.hatch 1, @drivers, (agent) =>
            agent.fromNode = agent.toNode = node.linkNeighbors().sample() #.sample()
            agent.face agent.toNode.position
            agent.v = @baseVelocity + u.randomFloat @velocityDelta
        null # avoid returning "for" results above
            
      step: ->
        console.log @animator.toString() if @animator.ticks % 100 is 0
        for driver in @drivers
          driver.face driver.toNode.position
          driver.forward(
            Math.min driver.v, driver.distance(driver.toNode.position))
          if .01 > driver.distance driver.toNode.position
            driver.fromNode = driver.toNode
            driver.toNode = driver.toNode.linkNeighbors().sample()
        null # avoid returning "for" results above

    model = new MyModel {
      div: "layers",
      patchSize: 13,
      hasNeighbors: false
    }
    model.debug() # Debug: Put Model vars in global name space
    model.start() # Run model immediately after startup initialization

    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
